buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
    }
}


apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'idea'

group = 'com.bertramlabs.plugins'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
sourceCompatibility = '1.7'
targetCompatibility = '1.7'



repositories {
	mavenLocal()
    jcenter()
    mavenCentral()

}

java {
    withSourcesJar()
    withJavadocJar()
}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    compileOnly 'org.codehaus.groovy:groovy-all:2.4.19'
    api project(':asset-pipeline-core')
    api 'org.mozilla:rhino:1.7R4'
    api 'org.slf4j:slf4j-api:1.7.28'
    api "com.github.sommeri:less4j:1.17.2"
    api "com.github.sommeri:less4j-javascript:0.0.1"

    testImplementation "org.spockframework:spock-core:1.3-groovy-2.4"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'less-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'less-asset-pipeline'
                    description 'Provides LESS Compiling support for the Asset-Pipeline library.'
                    url 'https://github.com/bertramdev/less-asset-pipeline'
                    scm {
                        url 'https://github.com/bertramdev/less-asset-pipeline'
                        connection 'scm:https://bertramdev@github.com/bertramdev/less-asset-pipeline.git'
                        developerConnection 'scm:git://github.com/bertramdev/less-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}



task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


